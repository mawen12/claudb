plugins {
  id 'java-library'
  id 'jacoco-report-aggregation'
  id 'test-report-aggregation'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version "${nexusVersion}"
  id 'com.github.ben-manes.versions' version "$versionsVersion"
}

dependencies {
  jacocoAggregation projects.claudbApp
  jacocoAggregation projects.claudbJunit4
  jacocoAggregation projects.claudbJunit5

  testReportAggregation projects.claudbApp
  testReportAggregation projects.claudbJunit4
  testReportAggregation projects.claudbJunit5
}

reporting {
  reports {
    testCodeCoverageReport(JacocoCoverageReport) {
      testType = TestSuiteType.UNIT_TEST
    }
    testAggregateTestReport(AggregateTestReport) {
      testType = TestSuiteType.UNIT_TEST
    }
  }
}

tasks.named('check') {
  dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
  dependsOn tasks.named('testAggregateTestReport', TestReport)
}

allprojects {
  apply plugin: 'java-library'
  apply plugin: 'jacoco'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  group = 'com.github.tonivade'
  
  ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

  jacoco {
    toolVersion = jacocoVersion
  }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    mavenLocal() 
    mavenCentral()
    maven {
      url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }

  test {
    useJUnitPlatform()

    jacoco {
      destinationFile = file("${buildDir}/jacoco/test.exec")
    }
  }

  jacocoTestReport {
    reports {
      xml.required = true
      html.required = true
      xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
  } 
}

subprojects {
  publishing {
    publications {
      mavenJava(MavenPublication) {
      
        if (project.name == 'claudb-lib') {
          artifactId = 'claudb'
        }
        
        from components.java
        
        pom {
          name = 'ClauDB'
          description = 'ClauDB is a REDIS implementation in Java'
          url = 'https://github.com/tonivade/claudb'
          inceptionYear = '2015'

          scm {
            url = 'https://github.com/tonivade/claudb'
            connection = 'scm:https://github.com/tonivade/claudb.git'
            developerConnection = 'scm:git://github.com/tonivade/claudb.git'
          }

          licenses {
            license {
              name = 'The MIT License (MIT)'
              url = 'https://github.com/tonivade/claudb/blob/master/LICENSE'
              distribution = 'repo'
            }
          }

          developers {
            developer {
              id = 'tonivade'
              name = 'Antonio Mu√±oz'
              email = 'antoniogmc@gmail.com'
            }
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.mavenJava
  }
  
  tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      
      stagingProfileId = '19a96b937cdb5'
      
      username = project.findProperty('nexusUsername')  
      password = project.findProperty('nexusPassword')
    }
  }
  
  clientTimeout = Duration.ofSeconds(300)
  connectTimeout = Duration.ofSeconds(60)

  transitionCheckOptions {
    maxRetries.set(40)
    delayBetween.set(java.time.Duration.ofMillis(3000))
  }
}
